/*
 * Copyright (c) 2011-2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
description = 'Reactor Spring Components'

ext {
	gradleVersion = '1.11'
	gradleScriptDir = "${rootProject.projectDir}/gradle"

	// Languages
	groovyVersion = '2.2.2'

	// Logging
	slf4jVersion = '1.7.6'
	logbackVersion = '1.1.1'

	// Libraries
	commonsIoVersion = '2.4'
	disruptorVersion = '3.2.1'
	jacksonDatabindVersion = '2.3.2'
	jsonPathVersion = '0.9.0'
	kryoVersion = '2.22'
	protobufVersion = '2.5.0'
	reactorVersion = '1.1.1.BUILD-SNAPSHOT'
	springVersion = '4.0.3.RELEASE'
	snappyVersion = '1.1.0.1'
	openHftChronicleVersion = '2.0.3'
	openHftLangVersion = '6.1.4'

	// Testing
	mockitoVersion = '1.9.5'
	spockVersion = '0.7-groovy-2.0'

	// Code coverage
	jacocoVersion = '0.7.0.201403182114'

	javadocLinks = [
			"http://docs.oracle.com/javase/7/docs/api/",
			"http://docs.oracle.com/javaee/6/api/",
			"http://reactor.github.io/docs/api/",
			"http://fasterxml.github.com/jackson-core/javadoc/2.3.2/",
			"http://docs.spring.io/spring/docs/4.0.x/javadoc-api/"
	] as String[]
}

buildscript {
	repositories {
		maven { url "http://repo.spring.io/plugins-release" }
	}
	dependencies {
		classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.4'
		classpath 'org.springframework.build.gradle:spring-io-plugin:0.0.3.RELEASE'
	}
}

apply from: "$gradleScriptDir/setup.gradle"

allprojects {
	apply plugin: 'idea'
	apply plugin: 'eclipse'

	configurations {
		jacoco
	}

	configurations.all {
		exclude group: "commons-logging"
		exclude module: "junit"
	}

	group = 'org.projectreactor.spring'
	repositories {
		//mavenLocal()
		maven { url 'http://repo.spring.io/libs-release' }
		maven { url 'http://repo.spring.io/libs-milestone' }
		maven { url 'http://repo.spring.io/libs-snapshot' }
		mavenCentral()
	}
}

subprojects { subproject ->
	apply plugin: 'java'
	apply plugin: 'groovy'
	apply plugin: 'maven'
	apply plugin: 'spring-io'

	sourceCompatibility = 1.7
	targetCompatibility = 1.7

	[compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:none", "-g"]

	sourceSets {
		test {
			resources {
				srcDirs = ['src/test/resources', 'src/test/java']
			}
		}
	}

	jar {
		manifest.attributes["Created-By"] = "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
		manifest.attributes["Implementation-Title"] = subproject.name
		manifest.attributes["Implementation-Version"] = subproject.version
	}

	task sourcesJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	task groovydocJar(type: Jar) {
		classifier = 'groovydoc'
		from groovydoc
	}

	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc
	}

	artifacts {
		archives sourcesJar
		archives javadocJar
		archives groovydocJar
	}

	// dependencies that are common across all java projects
	dependencies {
		// Logging
		compile "org.slf4j:slf4j-api:$slf4jVersion"

		// JSR-305 annotations
		optional "com.google.code.findbugs:jsr305:2.0.0"

		// Testing
		testCompile "org.codehaus.groovy:groovy-all:$groovyVersion",
				"org.spockframework:spock-core:$spockVersion",
				"org.hamcrest:hamcrest-library:1.3",
				"org.spockframework:spock-spring:$spockVersion",
				"org.springframework:spring-test:$springVersion"
		testRuntime "org.slf4j:jcl-over-slf4j:$slf4jVersion",
				"ch.qos.logback:logback-classic:$logbackVersion"

		// Code coverage
		jacoco "org.jacoco:org.jacoco.agent:$jacocoVersion:runtime"

		springIoVersions 'io.spring.platform:platform-versions:1.0.0.BUILD-SNAPSHOT@properties'
	}

	test {
		testLogging {
			jvmArgs "-javaagent:${configurations.jacoco.asPath}=destfile=${buildDir}/jacoco.exec,includes=reactor.spring.*"
			events "failed"
			exceptionFormat "full"
		}
	}
}

project('reactor-spring-core') {
	description = 'Reactor Spring Core Components'
	dependencies {
		// Reactor
		compile "org.projectreactor:reactor-core:$reactorVersion"

		// Spring
		compile "org.springframework:spring-beans:$springVersion",
				"org.springframework:spring-context:$springVersion",
				"org.springframework:spring-core:$springVersion",
				"org.springframework:spring-expression:$springVersion"
	}
}

project('reactor-spring-context') {
	description = 'Reactor Spring Context Components'
	dependencies {
		compile project(':reactor-spring-core')

		// Spring
		compile "org.springframework:spring-context-support:$springVersion"

		// JSON handling
		compile "com.jayway.jsonpath:json-path:$jsonPathVersion"
	}
}

project('reactor-spring-messaging') {
	description = 'Reactor Spring Messaging Components'
	dependencies {
		// Reactor Spring
		compile project(':reactor-spring-context'),
				"org.projectreactor:reactor-net:$reactorVersion"

		// Spring Messaging
		compile("org.springframework:spring-messaging:$springVersion") {
			exclude group: "org.projectreactor.spring", module: "reactor-net"
		}

		testCompile "com.esotericsoftware.kryo:kryo:$kryoVersion",
				"commons-io:commons-io:$commonsIoVersion"
	}
}

project('reactor-spring-webmvc') {
	description = 'Reactor Spring MVC Components'
	dependencies {
		// Reactor Spring
		compile project(':reactor-spring-context')

		provided 'javax.servlet:javax.servlet-api:3.1.0'

		// Spring MVC
		compile "org.springframework:spring-webmvc:$springVersion"
	}
}

configure(rootProject) {
	description = "Reactor Spring"

	task api(type: Javadoc) {
		group = "Documentation"
		description = "Generates aggregated Javadoc API documentation."
		title = "${rootProject.description} ${version} API"

		dependsOn {
			subprojects.collect {
				it.tasks.getByName("jar")
			}
		}
		options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
		options.author = true
		options.header = rootProject.description
		//options.overview = "src/api/overview.html"
		options.stylesheetFile = file("src/api/stylesheet.css")
		options.links(project.ext.javadocLinks)

		source subprojects.collect { project ->
			project.sourceSets.main.allJava
		}

		maxMemory = "1024m"
		destinationDir = new File(buildDir, "api")

		doFirst {
			classpath = files(subprojects.collect { it.sourceSets.main.compileClasspath })
		}
	}
}

task wrapper(type: Wrapper, description: "Create a Gradle self-download wrapper") {
	group = 'Project Setup'
	gradleVersion = "$gradleVersion"
}
